apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
  publications {
    aar(MavenPublication) {
      groupId GROUP
      version VERSION_NAME
      artifactId ARTIFACT_ID

      artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
      artifact androidJavadocsJar
      artifact androidSourcesJar
      pom.withXml {
        def dependencies = asNode().appendNode('dependencies')
        configurations.getByName("_releaseCompile").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
          def dependency = dependencies.appendNode('dependency')
          dependency.appendNode('groupId', it.moduleGroup)
          dependency.appendNode('artifactId', it.moduleName)
          dependency.appendNode('version', it.moduleVersion)
        }
      }
    }
  }
}

artifactory {
  contextUrl = "${artifactory_context_url}"
  publish {
    repository {
      repoKey = 'libs-release-local'

      username = "${artifactory_username}"
      password = "${artifactory_password}"
    }
    defaults {
      publications('aar')
      publishArtifacts = true

      properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
      publishPom = true
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  if (configurations.respondsTo("javadocDeps")) {
    classpath += configurations.javadocDeps
  } else {
    classpath += configurations.compile
  }
  if (JavaVersion.current().isJava8Compatible()) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }
  failOnError false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

artifactoryPublish.dependsOn('assembleRelease', 'generatePomFileForAarPublication', 'androidSourcesJar', 'androidJavadocsJar', 'androidJavadocs')